plugins {
    id 'com.android.library'
    id 'digital.wup.android-maven-publish' version '3.6.2'
    id "com.jfrog.bintray" version "1.8.4"
}

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 29
        versionName libraryVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        consumerProguardFile "proguard-rules.pro"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    api 'org.mozilla:rhino:1.7.11'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.yaml:snakeyaml:1.24'
    androidTestImplementation('com.google.caliper:caliper:1.0-beta-2') {
        exclude group: 'asm', module: 'asm'
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    source += files('build/generated/source/aidl/release') + files('build/generated/source/apt/release')
    javadoc.classpath += files(android.getBootClasspath().join(File.pathSeparator))
    options.with {
        linksOffline "http://d.android.com/reference", "${android.sdkDirectory.path}/docs/reference"
        links "http://mozilla.github.io/rhino/javadoc/"
    }
    doFirst {
        try {
            javadoc.classpath += files(android.libraryVariants.collect { variant -> variant.javaCompile.classpath.files }) + files('build/generated/source/r/release')
        } catch (e) {
            logger.warn('failed to get classpath for javadoc', e)
        }
    }
    afterEvaluate {
        dependsOn "assembleRelease"
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc
    classifier = 'javadoc'
}

ext.createPom = { pom ->
    pom.withXml {
        def xml = {
            name 'rhino-android'
            description 'Allows to use rhino on android'
            url 'https://github.com/F43nd1r/rhino-android'

            scm {
                connection 'scm:git:https://github.com/F43nd1r/rhino-android.git'
                developerConnection 'scm:git:git@github.com:F43nd1r/rhino-android.git'
                url 'https://github.com/F43nd1r/rhino-android.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'f43nd1r'
                    name 'Lukas Morawietz'
                }
            }
        }
        xml.resolveStrategy = DELEGATE_FIRST
        asNode().children().last() + xml
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.android
            groupId 'com.faendir.rhino'
            version libraryVersion
            artifact sourcesJar
            artifact javadocJar
            createPom(pom)
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // List all compile dependencies and write to POM
                configurations.implementation.getAllDependencies().each { dep ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return // ignore invalid dependencies

                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)

                    if (!dep.transitive) {
                        // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                        def exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        // Otherwise add specified exclude rules
                        def exclusionsNode = dependencyNode.appendNode('exclusions')
                        dep.properties.excludeRules.each { rule ->
                            def exclusionNode = exclusionsNode.appendNode('exclusion')
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = artifactoryUser
    key = artifactoryApiKey
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = 'rhino-android'
        websiteUrl = 'https://github.com/F43nd1r/rhino-android'
        vcsUrl = 'https://github.com/F43nd1r/rhino-android.git'
        licenses = ['The Apache Software License, Version 2.0']
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            desc = 'Allows to use rhino on android'
            mavenCentralSync {
                sync = true
                user = ossrhUser
                password = ossrhPassword
            }
        }
    }
}
bintrayUpload.dependsOn(build, javadocJar, sourcesJar, 'generatePomFileForMavenPublication')
